10 REM PRIM-3.BAS MATRICULACION DE PRIMERO 
20 NORMAL : HOME 
30 OPEN "R", #1, "B:CURSO.FCH",7 
40 FIELD#1,7 AS CUR$ 
50 GET#1,1 
60 IF CUR$<>"PRIMERO" THEN GOSUB 1080:CLOSE:GOTO 20 
70 CLOSE 
80 GOSUB 1150 
90 GOSUB 1230 
100 PRI$="S": REM CONECTAR IMPRESORA PRI$="S" 
110 GOTO 550 
120 REM PANTALLA 
130 NORMAL: HOME 
140 VTAB (1):HTAB (2):PRINT STRING$(77,"_") 
150 FOR I=2 TO 22 
160 VTAB (I) :HTAB (1):PRINT "|"
170 VTAB (I):HTAB (79):PRINT "|"
180 NEXT
190 VTAB (22):HTAB (2):PRINT STRING$(77,"_") 
200 VTAB (3):HTAB (25): INVERSE: PRINT "C L A V E S   D E   A L U M N O S":NORMAL 
210 FOR I=0 TO 14 
220 VTAB (XYT(I,2)) :HTAB (XYT(I,1)): PRINT LIT$(I) 
230 NEXT I 
240 RETURN 
250 REM ELECCION DE PARAMETROS 
260 GOSUB 120 
261 A$(10)="-": A$(12)="-": A$(13)="-" 
270 FOR I=0 TO 14: A$(I)="-"
280 IF I=11 AND A$(1)<>"S" THEN I=13:GOTO 450 
290 IF I=9 AND A$(8)<>"N" THEN I=10:GOTO 450 
300 IF I=12 AND A$(11)<>"S" THEN I=13: GOTO 450 
310 IF I=10 AND A$(9)<>"S" THEN 450 
320 INVERSE:VTAB (XYT(I,2)) :HTAB (XYT(I,1)): PRINT LIT$(I);: NORMAL: PRINT " ";
330 GOSUB 500 
340 IF S$="" THEN 320 
350 IF I=2 AND ((VAL(S$)<12 OR VAL(S$)>99)) AND S$<>"-" THEN 320 
360 IF I=3 AND S$<>"V" AND S$<>"M" AND S$<>"-" THEN 320 
370 IF 1=4 AND S$<>"R" AND S$<>"E" AND S$<>"-" THEN 320 
380 IF I=5 AND S$<>"I" AND S$<>"F" AND S$<>"-" THEN 320 
390 IF (I=1 OR I=7 OR I=6 OR I=9 OR I=11) AND S$<>"S" AND S$<>"N" AND S$<>"-" THEN 320 
400 IF I=8 AND S$<>"D" AND S$<>"N" AND S$<>"-" THEN 320 
410 IF I=10 AND S$<>"1" AND S$<>"2" AND S$<>"-" THEN 320 
420 IF I=1 4 AND (S$<"A" OR S$>"Z") AND S$<>" " AND S$<>"-" THEN 320 
430 A$(I)=S$ 
440 NORMAL :VTAB (XYT(I,2)): HTAB (XYT(I,1)): PRINT LIT$(I) 
450 NEXT I
460 VTAB (21) :HTAB (20) :PRINT "CONFORME (S=SI/N=NO) : "; 
470 GOSUB 1440:IF S$<>"S" AND S$<>"N" THEN 460 
480 IF S$="N" THEN 250 
490 RETURN 
500 IF I=0 THEN L%=35: GOSUB 1480 
510 IF I=2 THEN L%=2: GOSUB 1480 
520 IF I<>0 AND I<>2 AND I<>13 AND I<>12 THEN GOSUB 1440 
530 IF I=13 OR I=12 THEN L%=3: GOSUB 1480 
540 RETURN 
550 GOSUB 250 
560 NORMAL: HOME 
570 INVERSE:PRINT "I.B. J I M E N E Z   D E   L A   E S P A D A";:NORMAL:PRINT "         (CARTAGENA)" :PRINT: PRINT
580 PRINT "   A L U M N O S   D E   P R I M E R O"
590 PRINT
600 PRINT "NU.  REP  NU.F.       N O M B R E                    IDI. RE/ET APA EDAD GRUPO"
610 PRINT "==== === ====== ==================================== ==== ===== === ==== ====="
620 IF PRI$="S" THEN GOSUB 960 
630 Q=0 
640 GET#3, 1: N%=VAL(FIC$) 
650 FOR I=1 TO N% 
660 GET#2, I: V=VAL (ORD$) 
670 GET#1, V 
680 IF INSTR(A$(0),"-")=0 AND INSTR(NOM$, A$(0))=0 THEN 910 
690 IF A$(1)<>"-" AND REP$<>A$(1) THEN 910 
700 IF INSTR(A$(2), "-")=0 AND INSTR(EDAD$, A$(2))=0 THEN 910 
710 IF A$(3)<>"-" AND SEXO$<>A$(3) THEN 910 
720 IF A$(4)<>"-" AND REL$<>A$(4) THEN 910 
730 IF A$(5)<>"-" AND IDI$<>A$(5) THEN 910 
740 IF A$(6)<>"-" AND SEG$<>A$(6) THEN 910 
750 IF A$(7)<>"-" AND APA$<>A$(7) THEN 910 
760 IF A$(8)<>"-" AND DIU$<>A$(8) THEN 910 
770 IF A$(9)<>"-" AND FRAC$<>A$(9) THEN 910 
780 IF A$(10)<>"-" AND PAR$<>A$(10) THEN 910 
790 IF A$(11)<>"-" AND SUE$<>A$(11) THEN 910 
800 IF A$(12)<>"-" AND SU1$<>A$(12) AND SU2$<>A$(12) THEN 910 
810 IF A$(13)<>"-" AND SU2$<>A$(13) AND SU1$<>A$(13) THEN 910 
820 IF A$(14)<>"-" AND GRU$<>A$(14) THEN 910 
830 IF INSTR(NOM$,"%")<>0 THEN 910 
840 P$="UNO": IF PAR$="2" THEN P$="DOS" 
850 Q=Q+1: R$="( )":IF REP$="S" THEN R$="(R)" 
860 I$=" "+IDI$:IF SEG$="S" THEN I$="IF" 
870 IF FRAC$="S" THEN PRINT USING "#### ";Q;:PRINT R$;:PRINT USING " (####) ";V;:PRINT NOM$;"  (PAR. ";P$;")  ";APA$;"   ";EDAD$;"    ";GRU$:GOTO 900
880 IF SUE$="S" THEN  PRINT USING "#### ";Q;:PRINT R$;:PRINT USING " (####) ";V;:PRINT NOM$;"  (SUELTAS)   ";APA$;"   ";EDAD$;"    ";GRU$:GOTO 900
890 PRINT USING "#### ";Q;:PRINT R$;:PRINT USING " (####) ";V;:PRINT NOM$;"    ";I$;"   ";REL$;"    ";APA$;"   " EDAD$;"    ";GRU$
900 IF PRI$="S" THEN GOSUB 1050
910 IF INKEY$=CHR$(27) THEN 950
920 NEXT I
930 IF INKEY$="" THEN 930 
940 IF INKEY$<> CHR$(27) THEN 940 
950 CLOSE:RUN "PRIM-1.BAS"
960 LPRINT CHR$(27) ;"<";CHR$(27);"A";CHR$(27);"N";
970 WIDTH LPRINT 160
980 LPRINT CHR$(27) ;"!" ;CHR$(14);"I.B. JIMENEZ DE LA ESPADA";CHR$(15);CHR$(27);CHR$(34);"          (CARTAGENA) ":LPRINT :LPRINT CHR$(27);"Q"
990 LPRINT "A L U M N O S   D E   P R I M E R O"
1010 LPRINT
1020 LPRINT "NU.  REP NU.F.      N O M B R E                        IDI. RE/ET APA EDAD GRUPO"
1030 LPRINT "==== === ====== ====================================== ==== ===== === ==== ====="
1040 RETURN
1050 IF SUE$="S" THEN LPRINT USING "#### ";Q;:LPRINT R$;:LPRINT USING " (####) ";V;:LPRINT NOM$;"   (SUELTAS)    ";APA$;"   ";EDAD$;"    ";GRU$: RETURN
1060 IF FRAC$="S" THEN LPRINT USING "#### ";Q;:LPRINT R$;:LPRINT USING " (####) ";V;:LPRINT NOM$;"   (PAR. ";P$;")   ";APA$;"   ";EDAD$;"    ";GRU$: RETURN
1070 LPRINT USING "#### ";Q;:LPRINT R$; :LPRINT USING " (####) ";V;:LPRINT NOM$; "    ";I$;"     ";REL$;"    ";APA$;"   ";EDAD$;"    ";GRU$: RETURN
1080 REM DISCO MAL COLOCADO
1090 PRINT CHR$(7); CHR$(7) 
1100 VTAB (10): HTAB (10): INVERSE: PRINT "POR FAVOR, COLOQUE EL DISCO DE PRIMERO EN LA UNIDAD 'B'" 
1110 VTAB (12) :HTAB (30):PRINT "PULSE [RETURN]"
1120 IF INKEY$<>"" THEN 1120 
1130 IF INKEY$<>CHR$(13) THEN 1130 
1140 RETURN 
1150 REM DEFINICION DE FICHEROS 
1160 OPEN "R",#1,"B:PRIMERO.FCH" ,54
1170 FIELD#1,35 AS NOM$,2 AS EDAD$,1 AS SEXO$,1 AS REP$,1 AS IDI$,1 AS DIU$,1 AS REL$,1 AS APA$,1 AS GRU$,1 AS SEG$,1 AS FRAC$, 1 AS PAR$,1 AS SUE$,3 AS SU1$,3 AS SU2$
1180 OPEN "R",#2,"B:PRIMERO.NDX", 4
1190 FIELD#2,4 AS ORD$
1200 OPEN "R",#3,"B:PRIMERO.ORD" ,5
1210 FIELD#3,4 AS FIC$,1 AS CLA$
1220 RETURN
1230 REM INICIO DE MATRICES 
1240 DIM XYT(14,2), LIT$(14), A$(14) 
1250 FOR I=0 TO 14 
1260 READ XYT(I,1),XYT(I,2), LIT$(I) 
1270 NEXT I 
1280 RETURN 
1290 DATA 3,6,"NOMBRE Y APELLIDOS :"
1300 DATA 20,8,"ES REPETIDOR (S=SI/N=NO) :"
1310 DATA 3,10,"EDAD :"
1320 DATA 13,10,"SEXO (V=VARON/M=MUJER) :"
1330 DATA 40,10,"RELIGION-ETICA (R=REL./E=ET.) :"
1340 DATA 3,12,"IDIOMA <I=INGLES/F=FRANCES) :"
1350 DATA 40,12,"SEGUNDO IDIOMA (S=SI/N=NO) :"
1360 DATA 3,14,"PAGA A.P.A. (S=SI/N=NO) :"
1370 DATA 40,14, "NOCTURNO-DIURNO (N=NOCT./D=DIUR.) :"
1380 DATA 3,16,"FRACCIONA MATRICULA (S=SI/N=NO) :"
1390 DATA 40,16,"PARCIAL (1=UNO/2=DOS) :"
1400 DATA 3,18,"ASIGNATURAS SUELTAS (S=SI/N=NO) :"
1410 DATA 44,18,"ASIG. SUELTA 1a :"
1420 DATA 44,19,"ASIG. SUELTA 2a :"
1430 DATA 3,20,"GRUPO DE CLASE (A-Z) :"
1440 REM ENTRADA SIMPLE
1450 S$=INKEY$:IF S$<CHR$(32) OR S$>"Z" THEN 1450
1460 PRINT S$
1470 RETURN
1480 REM ENTRADA MULTIPLE
1490 P=0
1500 S$=""
1510 E$=INKEY$:IF E$="" THEN 1510
1520 IF E$=CHR$(13) THEN RETURN
1530 IF E$=CHR$(8) AND LEN(S$)>0 THEN HTAB (POS(0)-1):PRINT " ";:HTAB (POS(0)-1):P=P=1 :S$=LEFT$(S$,LEN(S$)-1):GOTO 1510
1540 IF (E$<" " OR E$>"Z") AND E$<>"ñ" AND E$<>"Ñ" THEN 1510
1550 IF P>=L% THEN 1510
1560 P=P+1:S$=S$+E$:PRINT E$; :GOTO 1510
